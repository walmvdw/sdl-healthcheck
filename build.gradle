buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'sdl-healthcheck'
    version =  '1.4.0'
}

bootRepackage {
    mainClass = 'com.markwal.sdl.healthcheck.HealthCheckApplication'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")

    //compile("org.springframework.boot:spring-boot-starter-actuator")
    //testCompile("junit:junit")

    compile("org.apache.httpcomponents:httpclient:4.5.2")
    compile("commons-io:commons-io:2.5")
    compile("com.google.code.gson:gson:2.7")
    compile("com.tridion:cd_common_util:8.1.1-1002")
    compile("org.apache.commons:commons-lang3:3.4")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

task dist(type: Zip, dependsOn: bootRepackage) {
    archiveName "${jar.baseName}-${jar.version}.zip"
    from('build/libs') {
        include '*.jar'
        into('lib')
    }

    from('src/dist') {
        expand(jarName: "${jar.baseName}-${jar.version}.jar")
        exclude('**/logback.xml')
        exclude('**/*.ps1')
        exclude('**/*.exe')
    }

    from('src/dist') {
        include('**/logback.xml')
        include('**/*.ps1')
        include('**/*.exe')
    }

    from('./') {
        include 'LICENSE'
        include 'README.md'
    }
}

dist.dependsOn(build)
